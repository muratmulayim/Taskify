grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

InkApp:
	globals += GlobalVariableExpression*
	constants += ConstantVariableExpression*
	tasks+=Task*
	entry = EntryTask
;

LogicalOperator:
	'AND' | 'OR'
;

ComparisionOperator:
	 '>=' | '<=' | '==' | '!=' | '<' | '>'
;

AssignmentOperator:
	'='
;

ArithmeticOperator:
	'-' | '+' | '/' | '*'
;

Operator:
	ArithmeticOperator | 
	ComparisionOperator | 
	LogicalOperator | 
	AssignmentOperator
;

BuiltinFunctions:
	'print'
;

BreakExpression:
	{BreakExpression}
	break = 'break'
;

terminal DOUBLE returns ecore::EDouble:
	INT '.' INT
;
	
terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false'
;

VariableType:
	{VariableType} 
	type = ('string' | 'integer' |  'boolean')
;

ArrayAssignment:
	'[' variable += Atomic (',' variable += Atomic)* ']'
;

VariableAssignment:
	{VariableAssignment} 
	expression = (OperationExpression | ArrayAssignment)
;

GlobalVariableExpression:
	{GlobalVariableExpression}
	'GLOBAL' variable = VariableDeclerationExpression
;

ConstantVariableExpression:
	{ConstantVariableExpression}
	'CONSTANT' type = VariableType name = ID (dimension = ArrayDimension)? operator = AssignmentOperator value = VariableAssignment
;

EntryTask:
	'entry' task = Task
;

Task:
	{Task}
	'task' name = ID '{'
		taskbody = TaskBody
	'}'
;

IfExpression:
	{IfExpression}
	'if' '(' ifcondition = OperationExpression ')' '{'
		ifbody = StatementBody
	'}' 
	('else' 'if' '(' elseifcondition += OperationExpression ')' '{'
		elseifbody += StatementBody
	'}')*
	('else' '{'
		elsebody = StatementBody
	'}')?
;

ForExpression:
	{ForExpression}
	'for' '(' initial = OperationExpression ';' test = OperationExpression ';' update = OperationExpression ')' '{'
		body = StatementBody
	'}' 
;

WhileExpression:
	{WhileExpression}
	'while' '(' test = OperationExpression ')' '{'
		body = StatementBody
	'}' 
;

BuiltinFunctionCallExpression:
	{BuiltinFunctionCallExpression}
	function = BuiltinFunctions '(' (parameters += Atomic (',' parameters += Atomic)*)? ')'
;

TaskBody:
	{TaskBody}
	body += VariableDeclerationExpression*
	body += BlockExpressions*
	('next' nexttask = [Task] | endtask = 'end')
;

StatementBody:
	{StatementBody} 
	body += BlockExpressions*
	(body += BreakExpression)?
;

BlockExpressions:
	OperationExpression |
	IfExpression |
	ForExpression |
	WhileExpression |
	BuiltinFunctionCallExpression
;

Expression:
	BreakExpression |
	BuiltinFunctionCallExpression |
	ConstantVariableExpression |
	ForExpression |
	GlobalVariableExpression|
	IfExpression |
	OperationExpression |
	VariableDeclerationExpression |
	WhileExpression
;

OperationExpression:
	Operation
;

Operation returns OperationExpression:
	Atomic ({Operation.left = current} (operator += Operator right += Atomic)*)
;

ArrayDimension:
	{ArrayDimension}
	'[' (index = [VariableSymbol] | size = INT) ']'
;

VariableDeclerationExpression:
	{VariableDeclerationExpression}
	type = VariableType name = ID (dimension = ArrayDimension)? (operator = AssignmentOperator value = VariableAssignment)?
;

VariableSymbol:
	VariableDeclerationExpression | ConstantVariableExpression
;

Atomic:
	{IntegerReference} value = INT |
	{DoubleReference} value = DOUBLE |
	{StringReference} value = STRING |
	{BooleanReference} value = BOOLEAN |
	{ArrayReference} variableReference = [VariableSymbol] (index = ArrayDimension) |
 	{VariableReference} variableReference = [VariableSymbol]
;


