grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


InkApp:
	shareds += SharedVariableExpression*
	constants += ConstantVariableExpression*
	tasks+=Task*
	entry = EntryTask
;

terminal DOUBLE returns ecore::EDouble:
	INT '.' INT
;
	
terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false'
;

BuiltinFunctions:
	'print'
;

LogicalOperator:
	'AND' | 'OR' | "AND_BITWISE" | "OR_BITWISE"
;

ComparisionOperator:
	 '>=' | '<=' | '==' | '!=' | '<' | '>'
;

AssignmentOperator:
	'='
;

ArithmeticOperator:
	'-' | '+' | '/' | '*'
;

Operator:
	ArithmeticOperator | 
	ComparisionOperator | 
	LogicalOperator
;

BreakExpression:
	{BreakExpression}
	break = 'break'
;

VariableType:
	{VariableType} 
	type = ('string' | 'integer' |  'boolean')
;


ArrayDimension:
	{ArrayDimension}
	'[' (index = [VariableSymbol] | size = INT) ']'
;

VariableSymbol:
	VariableDeclerationExpression | 
	ConstantVariableExpression |
	SharedVariableExpression
;

Variable:
	{ArrayReference} arrayReference = [VariableSymbol] (index = ArrayDimension) |
 	{VariableReference} variableReference = [VariableSymbol]
;

PrimitiveReference:
	{IntegerReference} value =  INT |
	{DoubleReference} value = DOUBLE |
	{StringReference} value = STRING |
	{BooleanReference} value = BOOLEAN
;

Atomic:
	PrimitiveReference | Variable
;

FunctionParameters:
	parameter += Atomic (',' parameter += Atomic)*
;

BuiltinPrintFunction:
	{BuiltinPrintFunction}
	'print' '(' parameters = FunctionParameters ')'
;

BuiltinRandomFunction:
	{BuiltinRandomFunction}
	'random' '(' ')'
;

BuiltinFunctionCallExpression:
	BuiltinPrintFunction |
	BuiltinRandomFunction
;

ArrayAssignment:
	'[' variable += Atomic (',' variable += Atomic)* ']'
;


/********************* Operation expressions ************************/
OperationExpression:
	Operation
;

Operation returns OperationExpression:
	Atomic ({Operation.left = current} (operator += Operator right += Atomic)*)
;


/********************* Assignment and Variable Declaration expressions ************************/
VariableAssignment:
	{VariableAssignment} 
	operator = AssignmentOperator expression = (OperationExpression | ArrayAssignment | BuiltinRandomFunction)
;

VariableAssignmentExpression:
	{VariableAssignmentExpression}
	variable = Variable assignment = VariableAssignment
;

VariableDeclerationExpression:
	{VariableDeclerationExpression}
	type = VariableType name = ID (dimension = ArrayDimension)? (assignment = VariableAssignment)?
;

SharedVariableExpression:
	{SharedVariableExpression}
	'SHARED' type = VariableType name = ID (dimension = ArrayDimension)?
;

ConstantVariableExpression:
	{ConstantVariableExpression}
	'CONSTANT' type = VariableType name = ID (dimension = ArrayDimension)? assignment = VariableAssignment
;


/********************* Loop expressions ************************/
IfExpression:
	{IfExpression}
	'if' '(' ifcondition = OperationExpression ')' '{'
		ifbody = StatementBody
	'}' 
	('else' 'if' '(' elseifcondition += OperationExpression ')' '{'
		elseifbody += StatementBody
	'}')*
	('else' '{'
		elsebody = StatementBody
	'}')?
;

ForExpression:
	{ForExpression}
	'for' '(' initial = OperationExpression ';' test = OperationExpression ';' update = OperationExpression ')' '{'
		body = LoopBody
	'}' 
;

WhileExpression:
	{WhileExpression}
	'while' '(' test = OperationExpression ')' '{'
		body = LoopBody
	'}' 
;


/********************* Task expressions ************************/
EntryTask:
	'entry' task = Task
;

Task:
	{Task}
	'task' name = ID '{'
		taskbody = TaskBody
	'}'
;

BlockExpressions:
	VariableAssignmentExpression |
	IfExpression |
	ForExpression |
	WhileExpression |
	BuiltinFunctionCallExpression
;


/********************* Body expressions ************************/
NextTaskExpression:
	('next' nexttask = [Task] | endtask = 'end')
;

TaskBody:
	{TaskBody}
	body += VariableDeclerationExpression*
	body += BlockExpressions*
	(body += NextTaskExpression)?
;

StatementBody:
	{StatementBody} 
	body += BlockExpressions*
	(body += BreakExpression)?
	(body += NextTaskExpression)?
;

LoopBody:
	{LoopBody}
	body += BlockExpressions*
;



Expression:
	BreakExpression |
	BuiltinFunctionCallExpression |
	ConstantVariableExpression |
	ForExpression |
	SharedVariableExpression|
	IfExpression |
	OperationExpression |
	VariableAssignmentExpression |
	VariableDeclerationExpression |
	WhileExpression |
	NextTaskExpression
;






